import thing
import actor
import geometry

Model:
    imports*=Import
    (
        (environment=Environment)?
        obstacles*=Obstacle
    )#
;

ThingPlacement:
    ref=[Thing:FQN] '@' pose=Pose
;

ActorPlacement:
    ref=[Actor:FQN] '@' pose=Pose
;

CompositePlacement:
    ref=[CompositeThing:FQN] '@' pose=Pose
;

ObstaclePlacement:
    ref=[Obstacle:FQN] '@' pose=Pose
;

Environment:
    'Environment:' name=ID
    (
        ('dimensions'
            (grid*=Grid)
        'end'
        )
        ('properties'
            (properties*=EnvironmentalProperty)
        'end'
        )
        ('things'
            ('sensors:'
                things*=ThingPlacement)?
            ('actuators:'
                things*=ThingPlacement)?
            ('composites:'
                things*=CompositePlacement)?
        'end'
        )?
        ('actors'
            (actors*=ActorPlacement)
        'end'
        )?
        ('obstacles'
            (obstacles*=ObstaclePlacement)
        'end'
        )?
    )#
    'end'
;

EnvDimensions: '2D' | '3D';

Grid:
    ('width:' width=FLOAT)
    ('height:' height=FLOAT)
    ('cellSizeCm:' cellSizeCm=FLOAT)
;

Obstacle:
    'Obstacle' name=ID
        'shape:' shape=Shape
    'end'
;

EnvironmentalProperty:
    (type=EnvPropertyType ':' value=FLOAT)
;

EnvPropertyType:
    'temperature' |
    'humidity'    |
    'luminosity'  |
    'sound'
;

FQN: ID('.'ID)*;
FQNI: ID+['.']('.*')?;
Import: 'import' importURI=STRING ('as' name=ID)?;

Description: /\#(.*?)$/;

Comment: CommentLine | CommentBlock ;

CommentLine: /\/\/.*?$/;

CommentBlock: /\/\*(.|\n)*?\*\//;