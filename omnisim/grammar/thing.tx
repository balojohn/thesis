import datatype
import geometry

Model:
    (
    imports*=Import
    thing=Thing
    )#
;

Thing: Sensor | Actuator | CompositeThing;

CompositeThing:
    'CThing' name=ID
	(
        ('shape' ':' shape=Shape)?
        ('sensors' ':'
             '-' sensors+=PosedSensor['-'])?
        ('actuators' ':'
             '-' actuators+=PosedActuator['-'])?
        ('composites' ':'
             '-' composites+=PosedCThing['-'])?
        ('dataModel:' dataModel=[DataType:FQN])?
	)#
    'end'
;

PosedThing: PosedSensor | PosedActuator | PosedCThing;

PosedCThing:
    ref=[CompositeThing:FQN|+m:thing] (transformation=DTransformation)?
;

PosedSensor:
    ref=[Sensor:FQN|+m:thing] ('('name=ID')')? (transformation=DTransformation)?
;

PosedActuator:
    ref=[Actuator:FQN|+m:thing] ('('name=ID')')? (transformation=DTransformation)?
;

Sensor:
    RangeFinder |
    Reader      |
    Alarm       |
    Microphone  |
    Light       |
    IMU
;

RangeFinder:
    'RangeFinder' name=ID
        (
            ('class:' class=ID)
            ('type:' type=[RangeFinderType])?
            ('dataModel:' dataModel=[DataType:FQN])
            ('shape:' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('hfov:' hfov=FLOAT)?
            ('vfov:' vfov=FLOAT)?
            ('distance:' distance=FLOAT)?
            ('minRange:' minRange=FLOAT)?
            ('maxRange:' maxRange=FLOAT)?
            ('noise:' noise=Noise)?
        )#
    'end'
;

RangeFinderType: 'Sonar' | 'IR';

LiDAR:
    'LiDAR' name=ID
        (
            ('class:' class=ID)
            ('dataModel:' dataModel=[DataType:FQN])
            ('shape:' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('distance:' distance=FLOAT)?
            ('minAngle:' minAngle=FLOAT)?
            ('maxAngle:' maxAngle=FLOAT)?
            ('numBeams:' numBeams=FLOAT)?
            ('minRange:' minRange=FLOAT)?
            ('maxRange:' maxRange=FLOAT)?
            ('noise:' noise=Noise)?
        )#
    'end'
;

Reader:
    'Reader' name=ID
        (
            ('class:' class=ID)
            ('type:' type=[ReaderType])
            ('dataModel:' dataModel=[DataType:FQN])
            ('shape:' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('distance:' distance=FLOAT)?
            ('resHeight:' resHeight=FLOAT)?
            ('resWidth:' resWidth=FLOAT)?
            ('hfov:' hfov=FLOAT)?
            ('vfov:' vfov=FLOAT)?
            ('noise:' noise=Noise)?
        )#
    'end'
;

ReaderType: 'Camera' | 'RFID' ;

Alarm:
    'Alarm' name=ID
        (
            ('class:' class=ID)
            ('type:' type=[AlarmType])
            ('dataModel:' dataModel=[DataType:FQN])
            ('shape:' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('triggered:' triggered=BOOL)?
            ('minRange:' minRange=FLOAT)?
            ('maxRange:' maxRange=FLOAT)?
            ('hz:' hz=FLOAT)?
            ('noise:' noise=Noise)?
        )#
    'end'
;

AlarmType: 'AreaAlarm' | 'LinearAlarm' ;

Microphone:
    'Microphone' name=ID
        (
            ('class:' class=ID)
            ('dataModel:' dataModel=[DataType:FQN])
            ('shape:' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('mode:' mode=STRING)?
            ('blocked:' blocked=BOOL)?
            ('noise:' noise=Noise)?
        )#
    'end'
;

Light:
    'Light' name=ID
        (
            ('class:' class=ID)
            ('dataModel:' dataModel=[DataType:FQN])
            ('shape' ':' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('luminoscity:' luminoscity=FLOAT)?
            ('color:' color=STRING)?
            ('minRange:' minRange=FLOAT)?
            ('maxRange:' maxRange=FLOAT)?
        )#
    'end'
;

IMU:
    'IMU' name=ID
        (
            ('class:' class=ID)
            ('type:' type=[IMUType])?
            ('dataModel:' dataModel=[DataType:FQN])
            ('shape:' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('noise:' noise=Noise)?
        )#
    'end'
;

IMUType: '6DOF' | '9DOF';

Actuator:
    PanTilt     |
    EnvDevice   |
    Relay       |
    Button      |
    Led         |
    Speaker
;

PanTilt:
    'PanTilt' name=ID
        (
            ('class:' class=ID)
            ('dataModel:' dataModel=[DataType:FQN])
            ('joints' ':' '-' joints*=Transformation['-'])?
            ('shape' ':' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
        )#
    'end'
;

EnvDevice:
    'EnvDevice' name=ID
        (
            ('class:' class=ID)
            ('type:' type=[EnvDeviceType] class=ID
                'with dispersion' dispersion=DispersionType)?
            ('dataModel:' dataModel=[DataType:FQN])
            ('shape' ':' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
        )#
    'end'
;

EnvDeviceType:
    'Temperature' |
    'Humidity'    |
    'PH'          |
    'Gas'
;

Relay:
    'Relay' name=ID
        (
            ('class:' class=ID)
            ('dataModel:' dataModel=[DataType:FQN])
            ('shape' ':' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            // ('maxFreq:' maxFreq=FLOAT)?
            ('state:' state=STRING)?
            ('allowed_states:' allowed_states=FLOAT)?
        )#
    'end'
;

Button: SingleButton | ButtonArray;

SingleButton:
    'SingleButton' name=ID
        (
            ('class:' class=ID)
            ('type:' type=[RangeFinderType])?
            ('dataModel:' dataModel=[DataType:FQN])
            ('shape' ':' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            // ('maxFreq:' maxFreq=FLOAT)?
            ('state:' state=INT)?
        )#
    'end'
;

ButtonArray:
    'ButtonArray' name=ID
        (
            ('class:' class=ID)
            ('size:' size=INT)
            ('dataModel:' dataModel=[DataType:FQN])
            ('shape' ':' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
        )#
    'end'
;

Led: SingleLed | LedArray;

SingleLed:
    'Led' name=ID
        (
            ('class:' class=ID)
            ('dataModel:' dataModel=[DataType:FQN])
            ('color' ':' color=INT)?
            ('shape' ':' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('responseTime:' responseTime=FLOAT)?
        )#
    'end'
;

LedArray:
    'LedArray' name=ID
        (
            ('class:' class=ID)
            ('size:' size=INT)
            ('dataModel:' dataModel=[DataType:FQN])
            ('shape' ':' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('responseTime:' responseTime=FLOAT)?
        )#
    'end'
;

Speaker:
    'Speaker' name=ID
        (
            ('class:' class=ID)
            ('dataModel:' dataModel=[DataType:FQN])
            ('shape' ':' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
        )#
    'end'
;

Noise:
    Gaussian | Uniform | CustomNoise
;

Gaussian:
    'Gaussian' '('
        'mean:' mean=FLOAT ',' 
        'std:' std=FLOAT
    ')'
;

Uniform:
    'Uniform' '('
        'min:' min=FLOAT ',' 
        'max:' max=FLOAT
    ')'
;

CustomNoise:
    'CustomNoise' '('
        'type:' type=STRING ',' 
        'params:' params=STRING
    ')'
;

DTransformation:
    ('@' transformation=Transformation)? ('on' parentThing=[PosedThing:FQN])?
;

DispersionType:
    Constant      |
    Linear        |
    Quadratic     |
    Exponential   |
    Logarithmic
;

Constant:
    'Constant' '(' 'value' ':' value=FLOAT ')'
;

Linear:
    'Linear' '('
        'start' ':' startingPoint=FLOAT ',' 
        'step' ':' step=FLOAT
    ')'
;

Quadratic:
    'Quadratic' '('
        'a' ':' a=FLOAT ',' 
        'b' ':' b=FLOAT ',' 
        'c' ':' c=FLOAT
    ')'
;

Exponential:
    'Exponential' '('
        'base' ':' base=FLOAT ',' 
        'yIntercept' ':' yIntercept=FLOAT
    ')'
;

Logarithmic:
    'Logarithmic' '('
        'base' ':' base=FLOAT ',' 
        'alpha' ':' alpha=FLOAT
    ')'
;
