import datatype
import geometry
import thing

Model:
    (
    imports*=Import
    actor=Actor
    )#
;

Actor:
    EnvActor        |
    SoundSource     |
    Color           |
    Text            |
    Human           |
    CompositeActor  |
    CustomActor       
;

CompositeActor:
    'CActor' name=ID
	(
        ('type:' type=ID)
        ('pubFreq:' pubFreq=FLOAT)?
        ('shape:' shape=Shape)?
        ('automated:' automated=BOOL)?
        ('targets:'
             '-' targets+=TargetPose['-'])?
        ('actors:'
             '-' actors+=PosedActor['-'])?
        ('composites:'
             '-' composites+=PosedCActor['-'])?
	)#
    'end'
;

TargetPose:
    point=Point | angle=Angle
;

PosedActor:
    (ref=[Actor:FQN]) ('('name=ID')')? (transformation=DTransformation)?
;

PosedCActor:
    (ref=[CompositeActor:FQN]) ('('name=ID')')? (transformation=DTransformation)?
;

PosedCustomActor:
    (ref=[CustomActor:FQN]) ('('name=ID')')? (transformation=DTransformation)?
;

CustomActor:
    'CustomActor' name=ID
        (
            ('class:' class=ID)?
            ('type:' type=ID)?
            ('subtype:' subtype=ID)?
            ('shape:' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('range:' range=FLOAT)?
            ('value:' value=FLOAT)?
            ('noise:' noise=Noise)?
            ('dispersion:' dispersion=DispersionType)?
            ('automated:' automated=BOOL)?
            ('affects:'
                 '-' affects+=ID['-'])?
            ('affectedBy:'
                 '-' affectedBy+=ID['-'])?
        )#
    'end'
;

EnvActor:
    'EnvActor' name=ID
        (
            ('class:' class=ID)
            ('type:' type=ID)?
            ('subtype:' subtype=[EnvActorType])?
            ('shape' ':' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('dispersion:' dispersion=DispersionType)?
            ('value:' value=FLOAT)?
            ('range:' range=FLOAT)?
        )#
    'end'
;

EnvActorType: 'Water' | 'Fire';

SoundSource:
    'SoundSource' name=ID
        (
            ('class:' class=ID)
            ('type:' type=ID)?
            ('shape' ':' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('range:' range=FLOAT)?
            ('sound' ':' sound=INT)?
            ('language' ':' language=STRING)?
            ('speech:' speech=STRING)?
            ('emotion:' emotion=STRING)?
        )#
    'end'
;

Color:
    'Color' name=ID
        (
            ('class:' class=ID)
            ('type:' type=ID)
            ('shape' ':' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('r' ':' r=INT)?
            ('g' ':' g=INT)?
            ('b' ':' b=INT)?
        )#
    'end'
;

Text:
    'Text' name=ID
        (
            ('class:' class=ID)
            ('type:' type=ID)?
            ('subtype:' subtype=[TextType])?
            ('shape' ':' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('message' ':' message=STRING)?
        )#
    'end'
;

TextType:
    'Barcode'     |
    'QR'          |
    'RFIDTag'     |
    'PlainText'
;

Human:
    'Human' name=ID
        (
            ('class:' class=ID)
            ('type:' type=ID)?
            ('shape' ':' shape=Shape)?
            ('pubFreq:' pubFreq=FLOAT)?
            ('sound' ':' sound=INT)?
            ('range' ':' range=INT)?
            ('motion' ':' motion=STRING)?
            ('language' ':' language=STRING)?
            ('speech:' speech=STRING)?
            ('emotion:' emotion=STRING)?
            ('age:' age=INT)?
        )#
    'end'
;